// Refer: https://docs.gradle.org/current/userguide/publishing_maven.html#publishing_maven:complete_example

plugins {
    // Produces a plain JAR
    id 'java-library'

    // Enables Spring-managed dependency versions (uses Spring's BOM)
    id 'io.spring.dependency-management'

    // Enables publishing to Maven repositories (local, GitHub Packages, OSSRH)
    id 'maven-publish'

    // Enables signing of artifacts (e.g., for OSSRH)
    id "signing"

    id 'org.jreleaser' version '1.18.0'
}
group = 'de.tum.cit.aet.helios'

java {
    // Use Java 21 as the compilation and runtime language level
    toolchain { languageVersion = JavaLanguageVersion.of(21) }

    withJavadocJar()
    withSourcesJar()
}

dependencyManagement {
    imports {
        // Pulls in Spring Boot's BOM to manage dependency versions
        mavenBom "org.springframework.boot:spring-boot-dependencies:3.4.4"
    }
}

repositories { mavenCentral() }

/* ---------- DEPENDENCIES ---------- */
/* Everything you actually need at runtime */
dependencies {
    // Required at runtime by the applications that use this library
    // Makes @ConfigurationProperties, lifecycle events, etc. available
    api 'org.springframework.boot:spring-boot-autoconfigure'
    api 'org.springframework:spring-context'

    // Required only during compilation of this library
    // Generates spring-autoconfiguration metadata files
    annotationProcessor 'org.springframework.boot:spring-boot-autoconfigure-processor'
    testAnnotationProcessor 'org.springframework.boot:spring-boot-autoconfigure-processor'

    // Required to send HTTP requests to Helios endpoints
    implementation 'com.squareup.okhttp3:okhttp:4.12.0'
    // Required to serialize Java objects (e.g., PushStatusPayload) to JSON
    implementation 'com.fasterxml.jackson.core:jackson-databind'

    // Provides metadata hints to IDEs and Spring tools for `application.yml` autocompletion
    // Only needed at build-time, not included in the final JAR
    compileOnly 'org.springframework.boot:spring-boot-configuration-processor'
    annotationProcessor 'org.springframework.boot:spring-boot-configuration-processor'

    // Lombok annotations (e.g., @Getter, @Builder) for internal use
    // Not included in final JAR — only required for compilation
    compileOnly 'org.projectlombok:lombok:1.18.30'
    annotationProcessor 'org.projectlombok:lombok:1.18.30'

    // Validation annotations (like @NotNull, @Positive)
    // This adds only the annotations, not the full validation engine
    compileOnly 'org.hibernate.validator:hibernate-validator'

    // SLF4J logging API — compileOnly since the user app provides an actual logging backend
    compileOnly 'org.slf4j:slf4j-api:2.0.17'

    // Required for annotations like @PostConstruct (@javax was moved to Jakarta in modern Java)
    implementation 'jakarta.annotation:jakarta.annotation-api:2.1.1'

    // Spring Boot test support for writing unit tests for your starter
    testImplementation 'org.springframework.boot:spring-boot-starter-test'
}

/* ---------- PUBLISHING ---------- */
publishing {
    publications {
        create("mavenJava", MavenPublication) {
            from components.java

            artifactId = "helios-status-spring-starter"

            versionMapping {
                allVariants {
                    fromResolutionResult()
                }
            }

            pom {
                // groupId = "de.tum.cit.aet.helios"
                groupId = "io.github.egekocabas"
                artifactId = "helios-status-spring-starter"
                name = groupId + ":" + artifactId
                description = "Helios starter for Spring Boot apps – push-based lifecycle status reporting."
                url = "https://github.com/ls1intum/Helios/tree/staging/server/helios-status-spring-starter"

                licenses {
                    license {
                        name = "MIT License"
                        url = "https://opensource.org/license/mit"
                        comments = "See https://github.com/ls1intum/Helios/blob/staging/LICENSE"
                    }
                }

                scm {
                    connection = "scm:git:git://github.com/ls1intum/Helios.git"
                    developerConnection = "scm:git:ssh://github.com/ls1intum/Helios.git"
                    url = "https://github.com/ls1intum/Helios"
                }

                developers {
                    developer {
                        id = "egekocabas"
                        name = "Ege Kocabas"
                        email = "kocabas.ege@tum.de"
                        organization = "Technical University of Munich - School of Computation, Information and Technology"
                        organizationUrl = "https://github.com/ls1intum"
                    }
                }
            }
        }
    }

    repositories {
        // GitHub Packages (requires GITHUB_TOKEN with write:packages)
        maven {
            name = "github"
            url = uri("https://maven.pkg.github.com/ls1intum/Helios")
            credentials {
                username = "x-oauth-basic"
                password = project.findProperty("githubToken") ?: System.getenv("GITHUB_TOKEN")
            }
        }

        maven {
            // Refer Migration from s01 OSSRH to staging: https://central.sonatype.org/publish/publish-portal-ossrh-staging-api/#configuration
            def releaseRepo = "https://ossrh-staging-api.central.sonatype.com/service/local/staging/deploy/maven2/"
            // Refer: https://central.sonatype.org/publish/publish-portal-snapshots/#publishing-with-the-central-publishing-maven-plugin
            def snapshotRepo = "https://central.sonatype.com/repository/maven-snapshots/"

            name = "ossrh-staging-api"
            url = version.endsWith('SNAPSHOT') ? snapshotRepo : releaseRepo

            // Refer: https://central.sonatype.org/publish/generate-portal-token/
            credentials {
                username = project.findProperty("ossrhUsername") ?: System.getenv("OSSRH_USERNAME")
                password = project.findProperty("ossrhPassword") ?: System.getenv("OSSRH_PASSWORD")
            }
        }
    }
}

signing {
    sign publishing.publications.mavenJava
}

javadoc {
    if(JavaVersion.current().isJava9Compatible()) {
        options.addBooleanOption('html5', true)
    }
}

test {
    // Enables JUnit 5 (used by Spring Boot Starter Test)
    useJUnitPlatform()
}
