import org.springframework.boot.gradle.tasks.run.BootRun

plugins {
    id 'java'
    id 'org.springframework.boot' version '3.3.5'
    id 'io.spring.dependency-management' version '1.1.6'
    id 'io.freefair.lombok' version '8.11'
    id 'org.springdoc.openapi-gradle-plugin' version '1.9.0'
    id 'org.openapi.generator' version '6.6.0'
}

group = 'de.tum.cit.aet'
version = '0.0.1-SNAPSHOT'

java {
    toolchain {
        languageVersion = JavaLanguageVersion.of(21)
    }
}

repositories {
    mavenCentral()
}

// Detect if the generateOpenApiDocs task is being executed
gradle.taskGraph.whenReady { taskGraph ->
    if (taskGraph.hasTask(':generateOpenApiDocs')) {
        println "Generating OpenAPI documentation..."
        // Add the H2 dependency dynamically
        dependencies {
            runtimeOnly 'com.h2database:h2'
        }
    }
}

dependencies {
    implementation 'org.springframework.boot:spring-boot-starter-data-jpa'
    implementation 'org.springframework.boot:spring-boot-starter-web'
    implementation 'org.springframework.boot:spring-boot-starter-security'
    implementation 'org.springdoc:springdoc-openapi-starter-webmvc-ui:2.6.0'
    implementation 'org.openapitools:jackson-databind-nullable:0.2.6'
    implementation 'io.nats:jnats:2.20.4'
    implementation 'org.kohsuke:github-api:1.326'
    implementation 'com.squareup.okhttp3:okhttp:4.12.0'
    implementation 'com.squareup.okhttp3:logging-interceptor:4.12.0'
    runtimeOnly 'org.postgresql:postgresql'
    testImplementation 'org.springframework.boot:spring-boot-starter-test'
    testImplementation 'org.springframework.security:spring-security-test'
    testRuntimeOnly 'org.junit.platform:junit-platform-launcher'
}

springBoot {
    mainClass = "de.tum.cit.aet.helios.HeliosApplication"
}

// Custom bootRun task to force the user to specify an active profile
tasks.named("bootRun") {
    doFirst {
        def activeProfile = System.getProperty("spring.profiles.active")
                ?: project.findProperty("spring.profiles.active")
                ?: System.getenv("SPRING_PROFILES_ACTIVE")
        if (!activeProfile) {
            throw new GradleException("""
    Error: 'spring.profiles.active' is not set.
    
    Please specify an active profile using one of the following methods:
    
    - Use a predefined task like 'bootRunDev'.
    - Set the 'SPRING_PROFILES_ACTIVE' environment variable.                
    - Use the '-Dspring.profiles.active=<profile>' JVM argument.
    
    Available profiles: dev, openapi (Don't use this profile directly).
    
    Examples:
    
    1. Using predefined task:
       ./gradlew bootRunDev
    
    2. Setting environment variable:
       export SPRING_PROFILES_ACTIVE=dev
       ./gradlew bootRun
    
    3. Passing JVM argument:
       ./gradlew -Dspring.profiles.active=dev bootRun
    
    4. Using project property:
       ./gradlew bootRun -Pspring.profiles.active=dev
                """
            )
        } else {
            println "Running with profile: ${activeProfile}"
            systemProperty "spring.profiles.active", activeProfile
        }
    }
}

tasks.register("bootRunDev", BootRun) {
    group = "application"
    description = "Runs the Spring Boot application with the dev profile"
    // Set the active profile to 'dev'
    systemProperty "spring.profiles.active", "dev"
    classpath = sourceSets.main.runtimeClasspath
    mainClass.set("de.tum.cit.aet.helios.HeliosApplication")
}


openApi {
    apiDocsUrl = 'http://localhost:8080/v3/api-docs.yaml'
    outputDir = file('.')
    outputFileName = 'openapi.yaml'
    // Set the active profile to 'openapi'
    customBootRun {
        args.set(["--spring.profiles.active=openapi"])
    }
}

tasks.named('test') {
    useJUnitPlatform()
}